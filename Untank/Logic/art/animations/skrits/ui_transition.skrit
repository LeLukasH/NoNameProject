// Animation Skrit: Simple_Transition
//
// This skrit sends no messages so its safe to call in the front end
// or a default_chore
//
// History:
//		VERSION 1.0:	Initial implementation

property int MAJORVERSION$ = 1;
property int MINORVERSION$ = 0;

owner = Aspect;
int	current_anim$;

////////////////////////////////////////////////////////////////////////////
// Utilities

RestartChore$ ( int newanim$ )
{ 
//	report.genericf("Anim: %d",newanim$);
	current_anim$ = newanim$;
	
	SendWorldMessage( WE_ANIM_SFX, owner.goid, owner.goid, 10*(newanim$ + 1) );
		
	owner.blender.ResetTimeWarp();

 	int numanims$ = owner.Blender.GetNumSubAnims();

	if (newanim$ >= numanims$) {
		newanim$ = 0;
	}

	int bg$ = owner.blender.OpenBlendGroup();
	owner.blender.AddAnimToBlendGroup(newanim$,1);
	owner.blender.CloseBlendGroup();
	owner.blender.SetBlendGroupWeight(bg$,1);

}

/////////////////////////////////////////////////////////////////////////////
// States

//----------------------------

startup state InitialState$ 
{

	event OnStartChore$ ( int subanim$, int /*flags$*/ )
	{
		RestartChore$ ( subanim$ ); 
		SetState LinearTransition$;
	}

}

//----------------------------

state LinearTransition$	
{
 
	event OnStartChore$ ( int subanim$, int /*flags$*/ )
	{
//		report.generic( "OnStart!\n" );
		RestartChore$ ( subanim$ );
 	}

	event OnUpdate$( float delta_t$ )
	{
 	 	int events$ = owner.UpdateBlender(delta_t$);

		if (AnimEventBitTest(events$,ANIMEVENT_SFX_1)) {
			SendWorldMessage( WE_ANIM_SFX, owner.goid, owner.goid, 1 + 10*(current_anim$ + 1) );
		}
		if (AnimEventBitTest(events$,ANIMEVENT_SFX_2)) {
			SendWorldMessage( WE_ANIM_SFX, owner.goid, owner.goid, 2 + 10*(current_anim$ + 1) );
		}
		if (AnimEventBitTest(events$,ANIMEVENT_SFX_3)) {
			SendWorldMessage( WE_ANIM_SFX, owner.goid, owner.goid, 3 + 10*(current_anim$ + 1) );
		}
		if (AnimEventBitTest(events$,ANIMEVENT_SFX_4)) {
			SendWorldMessage( WE_ANIM_SFX, owner.goid, owner.goid, 4 + 10*(current_anim$ + 1) );
		}
		if (AnimEventBitTest(events$,ANIMEVENT_HIDE_MESH)) {
			owner.SetHideMeshFlag(true);
		}
		if (AnimEventBitTest(events$,ANIMEVENT_SHOW_MESH)) {
			owner.SetHideMeshFlag(false);
		}
		if (AnimEventBitTest(events$,ANIMEVENT_FINISH)) {
			PostWorldMessage( WE_ANIM_DONE, owner.goid, owner.goid, owner.CurrentReqBlock, 0 );
			owner.AnimationCallback('done');
			SetState (HoldAtEnd$);
		}
 	}
}
 

//----------------------------

state HoldAtEnd$
{
	event OnStartChore$ ( int subanim$, int /*flags$*/ )
	{
		RestartChore$ ( subanim$ ); 
		SetState LinearTransition$;
 	}

}
