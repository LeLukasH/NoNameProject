// Animation Skrit: basic_fidget
//
// History:
//		VERSION 1.0:	Initial implementation
//		VERSION 2.0:	Converted to WorldMessages 
//						Added SFX event tests
//						Uses owner, rather than ASPECT arg
//		VERSION 2.1		Marked as not persistent
//
// Notes: This chore assumes we have the following
// animations to work with: (from the template GAS file)
//
//		00 = dff;
//		01 = dff-2;
//

property int MAJORVERSION$ = 2;
property int MINORVERSION$ = 0;
property bool _PERSIST$ = false;

owner = Aspect;

/////////////////////////////////////////////////////////////////////////////
// Per Skrit Object storage
			   
/////////////////////////////////////////////////////////////////////////////
// Utilities

RestartChore$ ( ) { 

	int numanims$ = owner.Blender.GetNumSubAnims();

	int newanim$ = 0;

	if (numanims$ > 0) {
		// pick a random animation 
		newanim$ = Math.RandomInt( 0,numanims$-1);
	} else {
		return;
	}

	owner.blender.ResetTimeWarp();

	int bg$ = owner.blender.OpenBlendGroup();
	owner.blender.AddAnimToBlendGroup(newanim$,1);
	owner.blender.CloseBlendGroup();
	owner.blender.SetBlendGroupWeight(bg$,1);

	owner.UpdateBlender(0);

}

/////////////////////////////////////////////////////////////////////////////
// States

//----------------------------

startup state LoopForever$ 
{

	event OnStartChore$ ( int /*subanim$*/, int /*flags$*/ )
	{

		RestartChore$ ( );

	}

	event OnUpdate$( float delta_t$ )
	{

 	 	int events$ = owner.UpdateBlender(delta_t$);

		if (AnimEventBitTest(events$,ANIMEVENT_SFX_1)) {
			PostWorldMessage( WE_ANIM_SFX, owner.goid, owner.goid,  1, 0 );
		}
		if (AnimEventBitTest(events$,ANIMEVENT_SFX_2)) {
			PostWorldMessage( WE_ANIM_SFX, owner.goid, owner.goid,  2, 0 );
		}
		if (AnimEventBitTest(events$,ANIMEVENT_SFX_3)) {
			PostWorldMessage( WE_ANIM_SFX, owner.goid, owner.goid,  3, 0 );
		}
		if (AnimEventBitTest(events$,ANIMEVENT_SFX_4)) {
			PostWorldMessage( WE_ANIM_SFX, owner.goid, owner.goid,  4, 0 );
		}
		if (AnimEventBitTest(events$,ANIMEVENT_HIDE_MESH)) {
			owner.SetHideMeshFlag(true);
		}
		if (AnimEventBitTest(events$,ANIMEVENT_SHOW_MESH)) {
			owner.SetHideMeshFlag(false);
		}
		if (AnimEventBitTest(events$,ANIMEVENT_FINISH)) {
			PostWorldMessage( WE_ANIM_DONE, owner.goid, owner.goid, owner.CurrentReqBlock, 0 );
		}


	}

}
