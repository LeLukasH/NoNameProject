//////////////////////////////////////////////////////////////////////////////
//
// File     :  spell_penalty.skrit
// Author(s):  Eric Tams
//
// Copyright © 2001 Gas Powered Games, Inc.  All rights reserved.
//----------------------------------------------------------------------------
//  $Revision:: $              $Date:$
//----------------------------------------------------------------------------
//
// any spell penalties that aren't easy to do through enchantments should go here.
//
//////////////////////////////////////////////////////////////////////////////

property bool 	effect_caster$		= false doc = "Apply penalty to caster instead of target.";
property float	decrease_health$	= 0.0	doc = "Amount to decrease health by 0-1.";

property string effect_script$		= ""	doc = "Name of the SiegeFx script that will be providing the visual.";
property string effect_params$		= ""   	doc = "Parameters to send to SiegeFx script";
property string description$		= ""	doc = "Description of enchantment being applied";
property string state_name$			= ""	doc = "Name of effect to use as a generic state and as a screen name.";
property bool	send_message$		= false doc = "Send the cast message in the message block to the target of the spell.";

owner = GoSkritComponent;
Goid 	target$;
Goid 	caster$;
float	spell_duration$;
SFxSID	active_effect$ = 0;

startup state CastBegin$
{
	transition -> RestoreState$ :  OnTimer( 1 );

	event OnGoHandleMessage$( eWorldEvent e$, WorldMessage msg$ )
	{
		if( e$ == WE_REQ_CAST ) {

			////////////////////
			// Normal Go
			
			caster$ = msg$.GetSendFrom();
			
			if( Owner.go.Parent != NULL )
			{
				// create Omni version of the spell.
				GoCloneReq cloneReq$ = MakeGoCloneReq( Owner.go.Parent.Goid, owner.goid );
				cloneReq$.Omni = true;
				Goid new_spell$ = GoDb.SCloneGo( cloneReq$ );
				
				// send Omni version an Activate to get it started.
				PostWorldMessage( WE_REQ_ACTIVATE, caster$, new_spell$, msg$.GetData1(), 0 );
			}
			
			// delete normal version of the spell.
			SetState Finished$;
		
		} else if ( e$ == WE_REQ_ACTIVATE ) {
			// figure out out targets.
			caster$ = msg$.GetSendFrom();
			Goid message_go$ = MakeGoid( msg$.GetData1() );
			
			// if the caster takes the penalty, then the caster is the target
			if( effect_caster$ )
			{
				target$ = msg$.GetSendFrom();
			} else {
				target$ = MakeGoid( msg$.GetData1() );
			}
			
			if( !target$.IsValidMp || !caster$.IsValidMp )
			{
				SetState RestoreState$;
				return;
			}
			
			// calculate how long the effect should last
			spell_duration$ = owner.Go.Magic.EvaluateEffectDuration(caster$.Go, target$.Go);
			
			// if we are already effected by this then bail.
			if( target$.Go.Actor.HasGenericState( state_name$ ) && (state_name$ != "" )) {
				
				report.generic("bail!\n");
				SetState( Abort$ );
			} else {
				
				// Start the visual
				
				if( effect_script$ != "" )
				{
					active_effect$ = SiegeFx.SRunScript( effect_script$, target$, caster$, effect_params$, owner.Goid, WE_DAMAGED );
				}
				
				if( send_message$ && message_go$.IsValid )
				{
					report.reportf("%s is getting healed by a pixie/fairy\n", message_go$.go.templatename );
					
					Report.SScreen( message_go$.Go.Player.MachineId, Report.Translate( owner.go.getmessage( "cast" ) ) );
				}
				
				// add status effect.
				target$.Go.Actor.SAddGenericState( state_name$, description$, spell_duration$, caster$, owner.goid, owner.go.magic.GetMagicLevel( caster$.go ) );
				
				// apply the enchantments.
				owner.go.magic.SApplyEnchantments( target$, caster$ );
				
				// apply the penalty
				if (decrease_health$ > 0.0)
				{
					float life$ = target$.Go.Aspect.CurrentLife;
					life$ *= decrease_health$;
					Rules.DamageGo(	target$, target$, target$, life$, true, true );
				}
				
				// create a timer so we know when to end the effect.
				this.CreateTimer( 1, spell_duration$ );
			}		
		}
	}
}

state RestoreState$
{
	event OnEnterState$
	{
		// If the go is still alive then restore his state
		if( target$.IsValid ) {
			// remove the state so this can be affected again
			target$.Go.Actor.SRemoveGenericState( state_name$ );
			
			if (active_effect$ != 0)
			{
				SiegeFx.SStopScript( active_effect$ );
			}
		}
		SetState( Finished$ );
	}
}

state Abort$
{
	event OnEnterState$
	{
		// Refund the players mana
		Rules.ChangeMana( caster$, owner.go.getmagic.manacost );

		SetState( Finished$ );
	}
}


state Finished$
{
	event OnEnterState$
	{
		// Delete this skrit
		PostWorldMessage( WE_REQ_DELETE, owner.Goid, owner.Goid, 2 );
	}
}