//////////////////////////////////////////////////////////////////////////////
//
// File     :  particle_emitter.skrit
// Author(s):  Rick Saenz, Eric Tams
//
// Copyright © 2000 Gas Powered Games, Inc.  All rights reserved.
//----------------------------------------------------------------------------
//  $Revision:: $              $Date:$
//----------------------------------------------------------------------------
//////////////////////////////////////////////////////////////////////////////

property bool   _server_only$		= 	false;

property bool	start_on_creation$	=	false	doc = "Start Emitter when it enters the world";

property float	red$				=	0.8		doc = "Red color intensity 0-1";
property float	green$				=	0.5		doc = "Green color intensity 0-1";
property float	blue$				=	0.0		doc = "Blue color intensity 0-1";

property float	xvel$				=	0.0		doc = "X component of initial velocity";
property float	yvel$				=	0.0		doc = "Y component of initial velocity";
property float	zvel$				=	40.0  	doc = "Z component of initial velocity";
property bool	random_vel$			=	false	doc = "Particles spawn with a random velocity";

property float	xacc$				=	0.0		doc = "X component of acceleration";
property float	yacc$				=	9.0		doc = "Y component of acceleration";
property float	zacc$				=	-10.0 	doc = "Z component of acceleration";

property float	fade$				=	0.45 	doc = "Amount to decrease particle alpha per second";
property int	count$				=	60		doc = "Maximum number of particles";

property bool	dark$				=	false	doc = "Enable dark blending method";
							
property float	particle_size$		=	1.75	doc = "Size of an individual particle";

property float	displace_min$		=	0.0		doc = "Min distance between start and end radii to determine cone angle";
property float	displace_max$		=	0.0		doc = "Max distance between start and end radii to determine cone angle";
property float	start_radius_min$	=	0.5		doc = "Inner start spawn radius - (spawn frustum)";
property float	start_radius_max$	=	0.5		doc = "Outer start spawn radius - (spawn frustum)";
property float	end_radius_min$		= 	15		doc = "Inner end spawn radius - (spawn frustum)";
property float	end_radius_max$		= 	25		doc = "Outer end spawn radius - (spawn frustum)";

property float	srate_start$		=	27.5	doc = "How fast to start spawning particles";
property float	srate_stop$			=	16.0	doc = "How fast to stop spawning particles";

property bool	ignite$				=	true	doc = "Ignite anything within collision volume";

property float	damage_min$			=	3.0		doc = "Minimum amount of damage to do per second";
property float	damage_max$			=	7.0		doc = "Maximum amount of damage to do per second";
property float	particle_fit$  		=	0.2		doc = "Emission volume fit for collision 0 is loose, 1 is tight";

property float	min_size_range$		=	-1.25	doc = "lower aditive range for random particle size generation";
property float	max_size_range$		=	1.25	doc = "upper aditive range for random particle size generation";
property float	growth$		  		=	.035	doc = "change in partice size over time";

owner = GoSkritComponent;

#include "k_inc_particle_emitter"

SFxSID active_script_id$ = 0;

startup state Constructed$
{
}

void StartEmitter$()
{
	if ( active_script_id$ == 0 )
	{
		// Run the generic_spawn script
		active_script_id$ =	SiegeFx.RunScript( "particle_emitter", owner.Goid, owner.Goid, params$, owner.Goid, WE_REQ_ACTIVATE );
	}
}

void StopEmitter$()
{
	if( active_script_id$ != 0 )
	{
		// Let the effect know that it should finish what it is doing and not spawn anything else
		SiegeFx.FinishScript( active_script_id$ );
	
		// Clear the script id
		active_script_id$ = null;
	}
}

trigger OnGoHandleMessage$( WE_ENTERED_WORLD )
{
	PackParams$();
	SetState Setup$;
}

trigger OnGoHandleMessage$( WE_SCIDBITS_CHANGED )
{
	SetState Setup$;
}

trigger OnGoHandleMessage$( WE_REQ_ACTIVATE )
{
	if( IsServerLocal )
	{
		Godb.SSetScidBit( owner.scid, 1, true );
	}
}

trigger OnGoHandleMessage$( WE_REQ_DEACTIVATE )
{
	if( IsServerLocal )
	{
		Godb.SSetScidBit( owner.scid, 1, false );
	}
}

state Setup$
{
	event OnEnterState$
	{
		if( StringTool.IsEmpty( params$ ) )
		{
			PackParams$();
		}
		if( !Godb.GetScidBit( owner.scid, 0 ) ) // nothing has changed. In the default state.
		{
			if( IsServerLocal )
			{
				if (start_on_creation$ == true)
				{
					int Bits$ = Godb.GetScidBits( owner.scid );
					Bits$ |= 1;
					Bits$ |= 2;
					Godb.SSetScidBits( owner.scid, Bits$ );
				}
				else
				{
					Godb.SSetScidBit( owner.scid, 0, true );
				}
			}
		}
		else if ( Godb.GetScidBit( owner.scid, 1 ) ) // emitter was active when it streamed out turn it back on.
		{
			StartEmitter$();
		} 
		else 
		{
			StopEmitter$();
		}
	}
}