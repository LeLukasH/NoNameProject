/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_face.skrit

	Author(s)	: 	Bartosz Kijanka

	Purpose		: 	Just face either an objcet or a position.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/


Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;


startup state STARTUP$
{
}


////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.GetGo;
	m_Mind$		= job$.GetGo.GetMind;
	m_Body$		= job$.GetGo.GetBody;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	SetState Facing$;
}


////////////////////////////////////////////////////////////////////////////////
//	global event handler
trigger OnWorldMessage$( WE_DESTRUCTED         ) { SetState Exiting$ ; }
trigger OnWorldMessage$( WE_JOB_DESTRUCTED     ) { SetState Exiting$ ; }


////////////////////////////////////////////////////////////////////////////////
//	
state Facing$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
		-> Exiting$:	OnWorldMessage( WE_MCP_INVALIDATED );
		-> Exiting$:	OnTimer( 1 );
	}

	event OnEnterState$
	{
		if( m_Job$.GoalObject.Go != NULL )
		{
			MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Job$.GoalObject );
		}
		This.CreateFrameTimer( 1, 2 );
	}
}


state Exiting$
{
	event OnEnterState$
	{
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );
		m_Job$.MarkForDeletion();
	}
}


