/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_gain_consciousness.skrit

	Author(s)	: 	Bartosz Kijanka

	Purpose		: 	Get up, punk.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/


Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;


startup state STARTUP$
{
}


////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.GetGo;
	m_Mind$		= job$.GetGo.GetMind;
	m_Body$		= job$.GetGo.GetBody;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	SetState Waking$;
}


state Waking$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_ANIM_DONE );
	}

	event OnEnterState$
	{
		int getupanim$ = m_Go$.Aspect.AspectHandle.Blender.GetSubAnimIndex(CHORE_MISC,'gtup');

		if (getupanim$ >= 0)
		{
		   eReqRetCode ret$ = MCPManager.MakeRequest( m_Go$.Goid, PL_PLAYANIM, CHORE_MISC, getupanim$, 0 );
		   if ( RequestFailed(ret$) )
		   {
			   SetState Exiting$;
			   return;
		   }
		}
		else
		{
			SetState Exiting$;
		}
	}
}


state Exiting$
{
	event OnEnterState$
	{
		m_Mind$.SDoJob( MakeJobReq( JAT_BRAIN, JQ_BRAIN, QP_CLEAR, AO_REFLEX ));
		m_Job$.MarkForDeletion();
	}
}


