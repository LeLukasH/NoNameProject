/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_die.skrit

	Author(s)	: 	Bartosz Kijanka

	Purpose		: 	Die gracefully.

					$$$ Here we can add code to find a nice spot to die, to wait
					for die anim to finish, before we actually switch state to
					dead.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/


Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;


startup state STARTUP$
{
}


////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.GetGo;
	m_Mind$		= job$.GetGo.GetMind;
	m_Body$		= job$.GetGo.GetBody;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	SetState Collapsing$;
}


state Collapsing$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_ANIM_DONE );
	}

	event OnEnterState$
	{
		if( m_Go$.Aspect.AspectHandle.CurrentChore == CHORE_DIE )
		{
			SetState Exiting$;
		}
		else
		{
			// Pick a Random animation to use
			eAnimStance stance$ = m_Go$.inventory.animstance;
			int subanim$ = m_Go$.Aspect.AspectHandle.Blender.GetNumSubAnims(CHORE_DIE,stance$);
			subanim$ = Math.RandomInt(0, Math.MaxInt( 0, subanim$-1 ) );

			MCPManager.Flush(m_Go$.Goid);
			MCPManager.MakeRequest( m_Go$.Goid, PL_DIE, subanim$ );
		}
	}
}


state Exiting$
{
	event OnEnterState$
	{
		m_Mind$.Clear();
		m_Job$.MarkForDeletion();
	}
}


