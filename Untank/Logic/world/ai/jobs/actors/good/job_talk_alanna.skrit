/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk.skrit

	Author(s)	: 	Bartosz Kijanka, David Tomandl

	Purpose		: 	Just stand there and talk at anyone we're told to.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;


#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee on a big dose of Seattle coffee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END );
	}

	event OnEnterState$
	{
		if ( GameAuditor.GetDb.GetBool( "bt_r1_skrubbs_killed" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "conversation_alanna_skrubb_dead" );
			TryAnim$('talk');
		} else {
			m_Go$.GetConversation.RSSelectConversation( "conversation_alanna" );
			TryAnim$('talk');
		}
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to 					  
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );	

		m_Job$.MarkForDeletion();
	}
}


