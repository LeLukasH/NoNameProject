/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_trella.skrit

	Author(s)	: 	David Tomandl, Eric Tams, Chad Queen, Bartosz Kijanka

	Purpose		: 	Custom talk job for Trella in multiplayer world, she
					should give a reward after the rune quest is finished.

	If you have completed the crystwind quest then trella should give you the stone if you don't have one.
	
	Currently, she says her quest_complete stuff if you have
	
	Completed the quest OR
	Have the rune in your party's inventory OR
	She has already rewarded you for bringing her the rune
	
	You get the reward if
	
	You are carrying the rune AND
	You have not been rewarded yet

	(C)opyright 2001 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

bool give_stone$( goid target$ )
{
	// see if the person we are tying to reward alread has the object in his inventory.
	Go item$ = m_Target$.Go.Inventory.GetItemFromTemplate( "stone_glb_crystwind_01" );
	if( item$ == null )
	{
		SiegePos SpawnPos$;
		GoCloneReq cloneReq$ = MakeGoCloneReq( "stone_glb_crystwind_01" );
		
		SpawnPos$ = target$.go.placement.position;
		cloneReq$.StartingPos	= SpawnPos$;
	    
		if( target$.Go.Hasmind )
		{
			if( AIQuery.FindSpotRelativeToSource( target$.Go, .25, .5, 2.0, target$.Go.Mind.TempPos1 ) )
	   		{
	   			cloneReq$.StartingPos = target$.Go.Mind.TempPos1;
	   		}
	   	}	
		cloneReq$.SnapToTerrain = true;
	    GoDb.SCloneGo( cloneReq$ );
	    
	    return true;
	}
	return false; 
}

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee on coffee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) = 
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
		-> Exiting$:	OnWorldMessage( WE_ANIM_DONE ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}

	event OnEnterState$
	{
		#only( game )
		[[
			Go rune$ = m_Target$.Go.Inventory.GetItemFromTemplate( "trella_rune" );
			
			if ( GameAuditor.GetDb.GetBool( "d_0x00c00349" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "zconversation_directions" );
				GameAuditor.GetDb.SetBool( "d_0x00c00349", false );
			}
			else if ((( rune$ != null  ) && ( !Victory.IsQuestActive( "quest_trella_rune" )) ) || ( GameAuditor.GetDb.GetBool( "trella_gave_reward_upper" )))
			{
				if (( rune$ != null  ) && ( !GameAuditor.GetDb.GetBool( "trella_gave_reward_upper" )) && ( GameAuditor.GetDb.GetBool( "talked_to_trella_with_rune" )))
				{
					// delete the rune from inventory
					
					m_Target$.Go.Mind.SDoJob( MakeJobReq( JAT_DROP, JQ_ACTION, QP_FRONT, AO_REFLEX, rune$.goid ) );
					PostWorldMessage( WE_REQ_DELETE, rune$.goid, rune$.goid, .1 );
					
					// reward the player
					
					if( m_Target$.IsValid() ) 
					{	
						give_stone$( m_Target$ );
					}
				}
				
				GameAuditor.GetDb.SetBool( "talked_to_trella_with_rune", true );
				if ( Victory.IsQuestActive( "quest_crystwind" ) ) {
					m_Go$.GetConversation.RSSelectConversation( "conversation_trella_with_rune_crystwind" );
				}
				else {
					m_Go$.GetConversation.RSSelectConversation( "conversation_trella_with_rune" );
				}
			}
			else if (( rune$ != null  ) || ( Victory.IsQuestCompleted( "quest_trella_rune" )))
			{
				if (( rune$ != null  ) && ( !GameAuditor.GetDb.GetBool( "trella_gave_reward" )) )
				{
					// delete the rune from inventory
					
					m_Target$.Go.Mind.SDoJob( MakeJobReq( JAT_DROP, JQ_ACTION, QP_FRONT, AO_REFLEX, rune$.goid ) );
					PostWorldMessage( WE_REQ_DELETE, rune$.goid, rune$.goid, .1 );
					
					// reward the player
					
					if( m_Target$.IsValid() ) 
					{	
						give_stone$( m_Target$ );
					}
				}
				
				if ( Victory.IsQuestCompleted( "quest_trella_rune" ) ) {
					if ( ( GameAuditor.GetDb.GetBool( "turn_world_red" ) ) && ( GameAuditor.GetDb.GetBool( "got_final_trella_dialogue" ) ) ) {
						m_Go$.GetConversation.RSSelectConversation( "conversation_trella_red" );
					}
					else {
						m_Go$.GetConversation.RSSelectConversation( "conversation_trella_goodbye_crystwind" );
						GameAuditor.GetDb.SetBool( "got_final_trella_dialogue", true );
					}
				}
				else if ( Victory.IsQuestActive( "quest_crystwind" ) ) {
					m_Go$.GetConversation.RSSelectConversation( "conversation_trella_quest_complete_crystwind" );
				}
				else {
					m_Go$.GetConversation.RSSelectConversation( "conversation_trella_quest_complete" );
				}
			}
			else if ( Victory.IsQuestActive( "quest_crystwind" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_trella_crystwind" );
			}
			else if ( GameAuditor.GetDb.GetBool( "turn_world_red" ) ) {
				m_Go$.GetConversation.RSSelectConversation( "conversation_trella_red" );
			} 
			else 
			{
				m_Go$.GetConversation.RSSelectConversation( "conversation_trella" );
			}
		]]		

		TryAnim$('talk');
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}
