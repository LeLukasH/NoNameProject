/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_konreid.skrit

	Author(s)	: 	Chad Queen, Bartosz Kijanka

	Purpose		: 	Just stand there and talk at anyone we're told to.

	(C)opyright 2001 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
		-> Exiting$:	OnWorldMessage( WE_ANIM_DONE ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
		
	}

	Trigger OnWorldMessage$( WE_REQ_ACTIVATE )
	{
		// drop the key!!
		dumpmain$;
	}

	event OnEnterState$
	{
		if ( GameAuditor.GetDb.GetBool( "met_king" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "conversation_king_secondary" );	
			TryAnim$('tlk1');		
		}
		else
		{
			m_Go$.GetConversation.RSSelectConversation( "conversation_king" );	
			GameAuditor.GetDb.SetBool( "met_king", true );
			TryAnim$('tlk1');
		}			
		
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// drop the key if we haven't already
		dumpmain$;
		
		m_Job$.MarkForDeletion();
	}
}