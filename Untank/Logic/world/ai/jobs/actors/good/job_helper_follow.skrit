/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_helper_follow.skrit

	Author(s)	: 	Eric Tams

	Purpose		: 	Follow another actor and help out if you can. Currently this
					Will try to transfer mana and pick up items. If the folower
					can do so.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/


Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;
float	m_FollowDistance$;
float	m_ApproachDistance$;
Goid 	last_item$;
Goid 	item$;

startup state STARTUP$
{
}

#include "k_job_c_mcp_path_utils"
#include "k_job_c_mcp_fidget_utils" 

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
	last_item$	= Goid.InValidGoid;
	item$		= Goid.InValidGoid;
	m_FollowDistance$		= 3.5;
	m_ApproachDistance$		= 2.0;		    		
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;
	
	ResetPathFindingParameters$();

	SetState RequestAction$;
}

////////////////////////////////////////////////////////////////////
//	global response to messages for my guardee
event OnCCWorldMessage$( eWorldEvent e$, WorldMessage /*msg$*/ )
{
	if( e$ == WE_KILLED )
	{
		SetState( Exiting$ );
	}
	else if( e$ == WE_LOST_CONSCIOUSNESS )
	{
		SetState( Exiting$ );
	}
}

trigger OnWorldMessage$( WE_ANIM_DONE )
{
	StartFidgetIfRequired$();
}



////////////////////////////////////////////////////////////////////////////////
//	
state RequestAction$
{
	transition
	{
		-> TargetMoved$:	OnWorldMessage( WE_MCP_DEPENDANCY_BROKEN );
		// We are switching to the fidget that is automatic upon arrival
		-> Arrived$:		OnWorldMessage( WE_MCP_CHORE_CHANGING );
		-> Arrived$:		OnWorldMessage( WE_ANIM_DONE );
		-> Arrived$:		OnWorldMessage( WE_MCP_SECTION_COMPLETED );
	}

	event OnEnterState$
	{
   
   		eReqRetCode ret$ = MCPManager.MakeRequest(
										m_Go$.Goid,
										PL_APPROACH, 
										m_Target$,
										m_LookAhead$, m_MaxApproachTime$,
										m_ApproachDistance$ ) ;

		if( RequestFailed(ret$) )
		{
			SetState( Arrived$ );
		}
		else if (ret$ == REQUEST_OK_BEYOND_RANGE)
		{
			SetState( MovingCloser$ );
		}
		else if (ret$ == REQUEST_OK)
		{
		}
		else // (ret$ == REQUEST_OK_IN_RANGE)
		{
				SetState( Arrived$ );
		}
	}
}

state Arrived$
{
	transition
	{
		-> Arrived$:		OnWorldMessage( WE_JOB_TIMER_DONE );
	}

	trigger OnTimer$( 1 )
	{
		SetState RequestAction$;
	}
	
	event OnEnterState$
	{
		m_Job$.SetTimer( m_Mind$.SensorScanPeriod );
		float dist$ = m_Mind$.GetDistanceAtPlanEnd( m_Target$.Go );

		m_Mind$.TempQtColl1.Clear;
		m_Mind$.TempQtColl1.Add( QT_ENEMY  );
		m_Mind$.TempQtColl1.Add( QT_ALIVE_CONSCIOUS );
		Go enemy$ = m_Mind$.GetClosestVisible( m_Mind$.TempQtColl1 );
		
		if( enemy$ != NULL )
		{
			m_Go$.Mind.SDoJob( MakeJobReq( JAT_FLEE_FROM_OBJECT, JQ_ACTION, QP_FRONT, AO_REFLEX, enemy$.goid ) );
		} 
		else
		{	
			// See if we have any items we want to give to the party.
			int num_items$ = 0;
			
			m_Mind$.TempGopColl1.Clear;
			m_Go$.Inventory.ListItems( IL_ALL, m_Mind$.TempGopColl1 );
			num_items$ = m_Mind$.TempGopColl1.size;
			if( num_items$ > 0 )
			{
				Job action$ = m_Mind$.GetFrontJob( JQ_ACTION );
				if( action$ != NULL && !World.IsMultiPlayer() )
				{
					// if we are already giving then bail.
					if( action$.JobAbstractType != JAT_GIVE )
					{
						Go temp_item$ = m_Mind$.TempGopColl1.Get( Math.RandomInt( 0, (m_Mind$.TempGopColl1.size - 1) ) );
						
						if( temp_item$ != NULL )
						{
							
							// look for mules, then just people to give our item to.			
							m_Mind$.TempGopColl1.Clear;
					
							AIQuery.Get( m_Go$, QT_PACK, m_Target$.Go.Parent.Children, m_Mind$.TempGopColl1 );
							AIQuery.Get( m_Go$, QT_NONPACK, m_Target$.Go.Parent.Children, m_Mind$.TempGopColl1 ); 
							
							int i$ = 0;
							int iend$ = m_Mind$.TempGopColl1.Size;
							bool gave$ = false;
							while( ( i$ < iend$ ) && !gave$ )
							{
								Go member$ = m_Mind$.TempGopColl1.get( i$ );
								if( member$ == NULL )
								{
									i$ += 1;
									continue;
								}
								
								if( member$.Inventory.CanAdd( temp_item$, IL_MAIN, true ) )
								{
									m_Mind$.SDoJob( MakeJobReq( JAT_GIVE, JQ_ACTION, QP_FRONT, AO_REFLEX, member$.goid, temp_item$.Goid ) );
									return;
								}
								i$ += 1;
							}
						}		
					}
				}
			}
			
			if( ( num_items$ < 1 ) || ( ( num_items$ < 35 ) && World.IsMultiPlayer() ) )
			{
				// look for loot.
				m_Mind$.TempGopColl1.Clear;
				m_Mind$.TempGopColl2.Clear;
				
				m_Mind$.GetVisible( QT_ITEM, m_Mind$.TempGopColl1);
				
				int i$ = 0;
				int iend$ = m_Mind$.TempGopColl1.Size();
				
				while( i$ < iend$ )
				{	
					Go temp_item$ = m_Mind$.TempGopColl1.Get( i$ );
					if( temp_item$.HasGui && temp_item$.IsSelectable && !temp_item$.HasActor )
					{
						m_Mind$.TempGopColl2.Add( temp_item$ );
					}
					i$ += 1;
				}
				
				if( m_Mind$.TempGopColl2.Size() > 0 )
				{
					item$ = m_Mind$.TempGopColl2.Get( Math.RandomInt( m_Mind$.TempGopColl2.Size() - 1 ) ).goid;
				}
        	}
			if( m_Mind$.ActorAutoXfersMana && !m_Go$.Inventory.IsSpellInProgress( "spell_mana_channel" ) && (m_Go$.Aspect.CurrentMana > 5))
			{
				Go spell$ = m_Go$.Inventory.GetSelectedSpell();
				
				//report.genericf( "Spell = %s\n",spell$.GetTemplateName());
				//report.genericf( "Mana = %g\n",m_Go$.Aspect.CurrentMana);
				if(spell$.GetTemplateName() == "spell_mana_channel")
				{
					m_Go$.Mind.SDoJob( MakeJobReq( JAT_CAST, JQ_ACTION, QP_FRONT, AO_REFLEX, m_Target$, spell$.Goid ) );
				}
			}
			else if( m_Mind$.ActorAutoHealsOthersLife && !m_Go$.Inventory.IsSpellInProgress( "spell_infuse_life" ) && (m_Go$.Aspect.CurrentMana > 5)) {
				Go spell$ = m_Go$.Inventory.GetSelectedSpell();
				
				//report.genericf( "Spell = %s\n",spell$.GetTemplateName());
				//report.genericf( "Mana = %g\n",m_Go$.Aspect.CurrentMana);
				if(spell$.GetTemplateName() == "spell_infuse_life")
				{
					m_Go$.Mind.SDoJob( MakeJobReq( JAT_CAST, JQ_ACTION, QP_FRONT, AO_REFLEX, m_Target$, spell$.Goid ) );
				}
			}
			else if( m_Mind$.ActorAutoPicksUpItems() && m_Mind$.ItemsVisible() && (item$ != last_item$) && item$.IsValid() )
			{
				if (!item$.Go.IsInsideInventory())
				{
					m_Mind$.SDoJob( MakeJobReq( JAT_GET, JQ_ACTION, QP_FRONT, AO_REFLEX, item$ ) );
					last_item$ = item$;
					item$ = Goid.InValidGoid;
				} 
			}
			else
			{
				m_Mind$.TempGopColl1.Clear;
				m_Mind$.TempGopColl2.Clear;
				
				m_Mind$.GetOccupantsInSphere( m_Mind$.SightRange, m_Mind$.TempGopColl1);
				int i$ = 0;
				Go temp_item$;
	
				while( i$ < m_Mind$.TempGopColl1.Size() )
				{
					temp_item$ = m_Mind$.TempGopColl1.Get( i$ );
					if( temp_item$.IsBreakable() && !temp_item$.IsActor() )
					{
						if( temp_item$.HasAspect )
						{
							if( !temp_item$.Aspect.IsInvincible && temp_item$.Aspect.IsSelectable )
							{
								m_Mind$.TempGopColl2.Add( temp_item$ );
							}
						}
					}
			        i$ += 1;
			    }
			    //report.genericf("size: %d\n", m_Mind$.TempGopColl2.Size());
			    if ( m_Mind$.TempGopColl2.Size() > 0 )
			    {
					m_Mind$.SDoJob( MakeJobReq(JAT_ATTACK_OBJECT, JQ_ACTION, QP_FRONT, m_Job$.GetOrigin, m_Mind$.TempGopColl2.Get( 0 ).goid, m_Go$.Goid ) );
			    } else {
			    	m_Mind$.TempGopColl2.Clear;
			    
			    	int i$ = 0;
					while( i$ < m_Mind$.TempGopColl1.Size() )
					{
						if(m_Mind$.TempGopColl1.Get( i$ ).IsContainer() && m_Mind$.TempGopColl1.Get( i$ ).IsUsable() && !m_Mind$.TempGopColl1.Get( i$ ).IsActor())
						{
							m_Mind$.TempGopColl2.Add( m_Mind$.TempGopColl1.Get( i$ ) );
						}
				        i$ += 1;
				    }
				    if ( m_Mind$.TempGopColl2.Size() > 0 )
				    {
			    		m_Mind$.SDoJob( MakeJobReq( JAT_USE, JQ_ACTION, QP_FRONT, AO_REFLEX, m_Mind$.TempGopColl2.Get( 0 ).goid ) );
				    } else {
				    	if( ( dist$ > m_FollowDistance$ ) && ( m_Mind$.GetDistance( m_Target$.Go ) > m_ApproachDistance$ ) )
						{
							this.CreateTimer( 1, .05 );
						}
					}
				}
			}
		}
	}
}

////////////////////////////////////////////////////////////////////////////////
//	
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}
