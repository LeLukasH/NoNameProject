/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_nonatoya.skrit

	Author(s)	: 	David Tomandl, Bartosz Kijanka, Chad Queen

	Purpose		: 	Talk using special anims.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('tak2');
	}

	event OnEnterState$
	{
		if ( GameAuditor.GetDb.GetBool( "nonataya_talked_to" ) )
		{
			TryAnim$('tak2');
			m_Go$.GetConversation.RSSelectConversation( "conversation_nonataya_two" );
		}
		else
		{
			GameAuditor.GetDb.SetBool( "nonataya_talked_to", true );
			TryAnim$('tak1');
			m_Go$.GetConversation.RSSelectConversation( "conversation_nonataya" );
		}		 

		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to 					  
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );	

		m_Job$.MarkForDeletion();
	}
}


