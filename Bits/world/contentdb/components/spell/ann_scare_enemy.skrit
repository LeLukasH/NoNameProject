//////////////////////////////////////////////////////////////////////////////
//
// File     :  ann_scare_enemy.skrit
// Author(s):  Emir Ghanawa
//
//----------------------------------------------------------------------------
//  $Revision:: $              $Date:$
//----------------------------------------------------------------------------
//
// Give commands to all enemies in radius of spell.
//
//////////////////////////////////////////////////////////////////////////////

property string effect_script$	= ""	doc = "Name of the SiegeFx script that will be providing the visual.";
property string script_params$	= ""   	doc = "Parameters to send to SiegeFx script";


owner = GoSkritComponent;

Goid target$;
Goid caster$;

////////////////////////////////////////
//	State Machine
			
startup state CastBegin$
{
 	event OnGoHandleMessage$( eWorldEvent e$, WorldMessage msg$ )
	{
		if( e$ == WE_REQ_CAST ) {
			target$ = MakeGoid( msg$.GetData1() );
			caster$ = msg$.GetSendFrom();
			
			if( !target$.IsValidMp || !caster$.IsValidMp )
			{
				SetState Finish$;
				return;
			}
			

            // Start the visual
            SiegeFx.SRunScript( effect_script$, target$, caster$, script_params$, owner.Goid, e$ );

            if( target$.go.Mind.UnderstandsJob( JAT_FLEE_FROM_OBJECT ) )
			{
				target$.go.Mind.SDoJob( MakeJobReq( JAT_FLEE_FROM_OBJECT, JQ_ACTION, QP_CLEAR, AO_COMMAND, caster$ ) );
			}

			SetState Finish$;
		} 
	}
}


state Finish$
{
	trigger OnGoHandleMessage$( WE_SPELL_SYNC_END )
	{
		SiegeFx.ClearVariables( owner.goid );
	
		PostWorldMessage( WE_REQ_DELETE, owner.Goid, owner.Goid, 1 );
	}
}
